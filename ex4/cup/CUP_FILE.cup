/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import AST.*;


parser code 
{:
	public Lexer lexer;
	public int error_code;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
		this.error_code = 0;
	}
	public void report_error(String message, Object info)
	{
		error_code = 1; /* Syntax error */
		System.out.print("ERROR >> ");		
		System.out.print("[");
		System.out.print(lexer.getLine());
		System.out.print(":");
		System.out.print(lexer.getTokenStartPosition());
		System.out.print("] ");	
		throw new java.lang.Error("syntax error!");
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	try
	{
		s = lexer.next_token();
	}
	catch (Error e)
	{
		this.error_code = 2; /* Lexical error */
		throw new java.lang.Error("lexical error!");
	}
	/* System.out.print(s.sym); */
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print(":");
	System.out.print(lexer.getTokenStartPosition());
	System.out.print("] ");
	System.out.print(TokenNames.terminalNames[s.sym]);
	if (s.value != null)
	{
		System.out.print("( ");
		System.out.print(s.value);
		System.out.print(" )");
	}
	System.out.print("\n");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal EQ;
terminal LT;
terminal GT;
terminal DOT;
terminal NIL;
terminal NEW;
terminal PLUS;
terminal COMMA;
terminal MINUS;
terminal TIMES;
terminal WHILE;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;
terminal CLASS;
terminal ARRAY;
terminal RETURN;
terminal EXTENDS;
terminal TYPE_INT;
terminal TYPE_VOID;
terminal TYPE_STRING;

/*************/
/* TERMINALS */
/*************/

terminal Integer INT;
terminal String STRING;
terminal String ID;
/*****************/
/* NON TERMINALS */
/*****************/
non terminal AST_PROGRAM Program;
non terminal AST_DEC dec;
non terminal AST_TYPE type;
non terminal AST_TYPEID typeID;
non terminal AST_ARRAYTYPEDEF arrayTypedef;
non terminal AST_VARDEC varDec;
non terminal AST_FUNCDEC funcDec;
non terminal AST_CLASSDEC classDec;
non terminal AST_EXP exp;
non terminal AST_VAR var;
non terminal AST_STMT stmt;
non terminal AST_NEWEXP newExp;
non terminal AST_CFIELD cField;
non terminal AST_LIST<AST_CFIELD> cFieldList;
non terminal AST_LIST<AST_STMT> stmtList;
non terminal AST_LIST<AST_TYPEID> typeIDList;
non terminal AST_LIST<AST_EXP> expList;
non terminal AST_LIST<AST_DEC> decList;


/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence nonassoc ASSIGN;
precedence left EQ;
precedence left LT, GT;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence nonassoc LBRACK;
precedence nonassoc LPAREN;
precedence left DOT;

/***********************/
/* START WITH: Program */
/***********************/
start with Program;

/********************/
/* DERIVATION RULES */
/********************/

Program ::= decList:dl				{: RESULT = new AST_PROGRAM(dl,dlleft); :};

decList ::= dec:d decList:dl			{: RESULT = new AST_LIST<AST_DEC>(d,dl,dleft); :}
	|   dec:d				{: RESULT = new AST_LIST<AST_DEC>(d,null,dleft); :};

dec	::= varDec:v				{: RESULT = new AST_DEC_VARDEC(v,vleft); :}
	|   funcDec:f				{: RESULT = new AST_DEC_FUNCDEC(f,fleft); :}
	|   classDec:c				{: RESULT = new AST_DEC_CLASSDEC(c,cleft); :}
	|   arrayTypedef:a			{: RESULT = new AST_DEC_ARRAYTYPEDEF(a,aleft); :};

type 	::= TYPE_INT:x			{: RESULT = new AST_TYPE(0,xleft); :}
	|   TYPE_STRING:x         		{: RESULT = new AST_TYPE(1,xleft); :}
	|   TYPE_VOID:x           		{: RESULT = new AST_TYPE(2,xleft); :}
	|   ID:t                		{: RESULT = new AST_TYPE(t,tleft); :};

arrayTypedef ::= ARRAY ID:name EQ type:t LBRACK RBRACK SEMICOLON  	{: RESULT = new AST_ARRAYTYPEDEF(name,t,tleft); :};

varDec ::= type:t ID:name SEMICOLON 				{: RESULT = new AST_VARDEC(t,name,null,null,tleft); :}
    	|  type:t ID:name ASSIGN exp:e SEMICOLON 			{: RESULT = new AST_VARDEC(t,name,e,null, tleft); :}
    	|  type:t ID:name ASSIGN newExp:n SEMICOLON 		{: RESULT = new AST_VARDEC(t,name,null,n, tleft); :};
     
funcDec ::= type:t ID:name LPAREN RPAREN LBRACE stmtList:sl RBRACE 		{: RESULT = new AST_FUNCDEC(t,name,null,sl,tleft); :}
    |       type:t ID:name LPAREN typeIDList:ti RPAREN LBRACE stmtList:sl RBRACE {: RESULT = new AST_FUNCDEC(t,name,ti,sl,tleft); :};

stmtList ::= stmt:s stmtList:sp 			{: RESULT = new AST_LIST<AST_STMT>(s,sp,sleft); :}
    |        stmt:s 					{: RESULT = new AST_LIST<AST_STMT>(s,null,sleft); :};

typeID ::= type:t ID:name				{: RESULT = new AST_TYPEID(t,name,tleft); :};

typeIDList ::= typeID:ti COMMA typeIDList:til 		{: RESULT = new AST_LIST<AST_TYPEID>(ti,til,tileft); :}
    |	typeID:ti					{: RESULT = new AST_LIST<AST_TYPEID>(ti,null,tileft); :};
    
expList ::= exp:e COMMA expList:el			{: RESULT = new AST_LIST<AST_EXP>(e,el,eleft); :}
    |	exp:e 						{: RESULT = new AST_LIST<AST_EXP>(e,null,eleft); :};

classDec ::= CLASS ID:name LBRACE cFieldList:cl RBRACE 			{: RESULT = new AST_CLASSDEC(name,null,cl,nameleft); :}
    |   CLASS ID:name EXTENDS ID:parentClass LBRACE cFieldList:cl RBRACE 	{: RESULT = new AST_CLASSDEC(name,parentClass,cl,nameleft); :};

cFieldList ::= cField:c cFieldList:cl 			{: RESULT = new AST_LIST<AST_CFIELD>(c,cl,cleft); :}
    |   cField:c 					{: RESULT = new AST_LIST<AST_CFIELD>(c,null,cleft); :};

exp ::= var:v 						{: RESULT = new AST_EXP_VAR(v,vleft); :}
    |   LPAREN exp:e RPAREN 				{: RESULT = new AST_EXP_PAREN(e,eleft); :}
    |   exp:e1 PLUS exp:e2 				{: RESULT = new AST_EXP_BINOP(e1,e2,0,e1left); :}
    |   exp:e1 MINUS exp:e2 				{: RESULT = new AST_EXP_BINOP(e1,e2,1,e1left); :}
    |   exp:e1 TIMES exp:e2 				{: RESULT = new AST_EXP_BINOP(e1,e2,2,e1left); :}
    |   exp:e1 DIVIDE exp:e2 				{: RESULT = new AST_EXP_BINOP(e1,e2,3,e1left); :}
    |   exp:e1 LT exp:e2 				{: RESULT = new AST_EXP_BINOP(e1,e2,4,e1left); :}
    |   exp:e1 GT exp:e2 				{: RESULT = new AST_EXP_BINOP(e1,e2,5,e1left); :}
    |   exp:e1 EQ exp:e2 				{: RESULT = new AST_EXP_BINOP(e1,e2,6,e1left); :}
    |   var:v DOT ID:name LPAREN expList:el RPAREN 	{: RESULT = new AST_EXP_FUNC(v,name,el,nameleft); :}
    |   var:v DOT ID:name LPAREN RPAREN 		{: RESULT = new AST_EXP_FUNC(v,name,null,nameleft); :}
    |   ID:fname LPAREN expList:el RPAREN 		{: RESULT = new AST_EXP_FUNC(null,fname,el,fnameleft); :}
    |   ID:fname LPAREN RPAREN 				{: RESULT = new AST_EXP_FUNC(null,fname,null,fnameleft); :}
    |   NIL:i					{: RESULT = new AST_EXP_NIL(ileft); :}
    |   STRING:s					{: RESULT = new AST_EXP_STRING(s,sleft); :}
    |   INT:i					{: RESULT = new AST_EXP_INT(i, ileft); :}
    |   MINUS INT:i					{: RESULT = new AST_EXP_INT(-i, ileft); :};

var ::= ID:name						{: RESULT = new AST_VAR_SIMPLE(name,nameleft); :}
    |   var:v DOT ID:name				{: RESULT = new AST_VAR_FIELD(v,name,nameleft); :}
    |   var:v LBRACK exp:e RBRACK			{: RESULT = new AST_VAR_SUBSCRIPT(v,e,vleft); :};

stmt ::= varDec:v						{: RESULT = new AST_STMT_VARDEC(v,vleft); :}
    |   var:v ASSIGN exp:e SEMICOLON				{: RESULT = new AST_STMT_ASSIGN(v,e,vleft); :}
    |   var:v ASSIGN newExp:n SEMICOLON				{: RESULT = new AST_STMT_ASSIGN_NEW(v,n,vleft); :}
    |   RETURN:x SEMICOLON					{: RESULT = new AST_STMT_RETURN(null,xleft); :}
    |   RETURN exp:e SEMICOLON					{: RESULT = new AST_STMT_RETURN(e,eleft); :}
    |   IF LPAREN exp:e RPAREN LBRACE stmtList:sl RBRACE	{: RESULT = new AST_STMT_IF(e,sl,eleft); :}
    |   WHILE LPAREN exp:e RPAREN LBRACE stmtList:sl RBRACE	{: RESULT = new AST_STMT_WHILE(e,sl,eleft); :}
    |   var:v DOT ID:name LPAREN expList:el RPAREN SEMICOLON	{: RESULT = new AST_STMT_FUNC(v,name,el,nameleft); :}
    |   var:v DOT ID:name LPAREN RPAREN SEMICOLON		{: RESULT = new AST_STMT_FUNC(v,name,null,nameleft); :}
    |   ID:name LPAREN expList:el RPAREN SEMICOLON		{: RESULT = new AST_STMT_FUNC(null,name,el,nameleft); :}
    |   ID:name LPAREN RPAREN SEMICOLON				{: RESULT = new AST_STMT_FUNC(null,name,null,nameleft); :};

newExp ::= NEW type:t 					{: RESULT = new AST_NEWEXP(t,null,tleft); :}
    |   NEW type:t LBRACK exp:e RBRACK			{: RESULT = new AST_NEWEXP(t,e,tleft);    :};

cField ::= varDec:v 		{: RESULT = new AST_CFIELD_VARDEC(v,vleft);  :}
    |   funcDec:f		{: RESULT = new AST_CFIELD_FUNCDEC(f,fleft); :};

